buildscript {      // Configuration for building
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.4'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' apply true
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.20'
}

apply plugin: 'com.google.protobuf'

addCommonLibs(project)

dependencies {
    implementation "com.google.protobuf:protobuf-java-util:$protobufVersion"
    api "com.google.protobuf:protobuf-kotlin:$protobufVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.8.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:1.8.0'
    implementation "io.grpc:grpc-api:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-kotlin-stub:1.4.1"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.+'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:1.4.1:jdk8@jar"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            grpckt {}
        }
        all()*.builtins {
            kotlin {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/source/proto/main/java", "$buildDir/generated/source/proto/main/grpc"
        }
        kotlin {
            srcDirs "$buildDir/generated/source/proto/main/kotlin", "$buildDir/generated/source/proto/main/grpckt"
        }

    }
}

