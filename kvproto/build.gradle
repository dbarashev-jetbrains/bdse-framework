buildscript {      // Configuration for building
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.+'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' apply true
}

apply plugin: 'com.google.protobuf'

version '2024.02.07'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java-util:3.22.2'
    api 'com.google.protobuf:protobuf-kotlin:3.22.2'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0"
    implementation "io.grpc:grpc-api:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-kotlin-stub:1.+"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

protobuf {
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.+'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:1.2.1:jdk7@jar"
        }
    }
    generateProtoTasks {
        all().forEach {
            it.plugins {
                grpc {}
                grpckt {}
            }
            it.builtins {
                kotlin {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/source/proto/main/java", "$buildDir/generated/source/proto/main/grpc"
        }
        kotlin {
            srcDirs "$buildDir/generated/source/proto/main/kotlin", "$buildDir/generated/source/proto/main/grpckt"
        }

    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
    kotlinOptions.freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn"]
}

test {
    useJUnitPlatform()
}
